label ch1ep1:

    fox "*Меня охватил ужас. Если меня схватят, то повесят.*"
    fox "*По моей информации, шахта, идущая через пещеру, была давно заброшена.*"
    menu:
        fox "*Но прямо сейчас мимо расщелины, где я спрятался, идёт несколько солдафонов и исследователь.*"
        "Задержать дыхание и не шевелиться":
            fox "*Голоса позади меня стихли. Нужно поспешить найти руины.*"
        "Попробовать прошмыгнуть":
            fox "*Скрытен, как зверь на охоте.*"

    "" "Некоторое время спустя..."
    fox "*Вот мой ориентир - магическая руна на перекрестке трёх проходов. Мне нужно пройти 50 шагов по крайнему справа из трёх.*"
    fox "*48...49...50. Бинго! Здесь определённо что-то есть.*"
    fox "*Я достаю артефакт Инфров. Достаточно приложить его к нужному месту, и проход в руины будет открыт.*"
    fox "Кхе-кхе..."
    fox "*Ох, отмываться от пылищи буду ещё неделю!*"

    call ch1ep1fz1

    fox "*Вокруг полки со свитками. В центре комнаты виднеется пьедестал.*"
    menu:
        "Изучить свитки на полках":
            fox "*Первый взятый мною свиток рассыпался прямо у меня в руках. Вернусь к исследованию пьедестала.*"
        "Изучить пьедестал":
            pass

    fox "*На пьедестале покоится магическое хранилище со свитками.*"
    fox "*Здесь написано <<Природа магии>>. Об Инфрах ходят легенды, что они могли колдовать без элизия.*"
    fox "*По мне так брехня, но много кто верит.*"
    fox "*Если свитки сохранились, то это обеспечит меня безбедной жизнью в Летающем городе.*"
    
    menu:
        fox "*Система хитроумная, но я уже работал с такими устройствами.*"
        "Пора вскрыть хранилище!":
            pass
    "" "*Клац*"
    fox "*Хранилище открыто. Передо мной не тронутые временем свитки. Удача улыбнулась мне.*"
    menu:
        fox "*За это можно выручить много золотых монет.*"
        "Продолжить исследование":
            pass
    
    menu:
        fox "*М-м-м-м, звон золотых монет ни с чем не перепутать.*"
        "Продолжить исследование!":
            pass
    menu:
        fox "*Он у них не такой, как у серебряных или медных, словно медовуха для души.*"
        "Продолжить исследование!!!":
            pass
    fox "*Что-то я размечтался.*"

    return

label ch1ep1fz1:
    $ wantToExit = False
    $ findExit = False
    $ visitedPlaces = set()
    while not wantToExit:
        menu:
            fox "*Передо мной перекрёсток. Куда же мне пойти?*"
            "Пойти налево в неприметный коридор":
                $ visitedPlaces.add("left")
                fox "Коридор ведёт к жилым комнатам"
                menu:
                    "Вернуться обратно":
                        pass
            "Пойти прямо в хорошо украшенный коридор" if "center" not in visitedPlaces:
                fox "*Думаю, я смогу найти здесь что-то интересное.*"
                $ visitedPlaces.add("center")
                if len(visitedPlaces & {"left", "right"}) == 2:
                        fox "*Я уже изучил другие проходы.*"
                menu:
                    fox "*Это библиотека. Хочу ли я исследовать её прямо сейчас?*"
                    "Вперёд!":
                        $ wantToExit = True
                    "Хочу ещё побродить":
                        pass
            "Пойти в библиотеку" if "center" in visitedPlaces:
                $ wantToExit = True
            "Пойти направо в полуразрушенный коридор":
                $ visitedPlaces.add("right")
                fox "*Раньше тут была достаточно просторная комната с аркой портала посередине, но сейчас всё в обломках.*"
                menu:
                    "Вернуться обратно":
                        pass
    return  

label ch1ep2:
    default rush_1 = False
    menu:
        fox "*Тут неприметная дверь в углу.*"
        "Изучить дверь":
            pass
        "Ворваться внутрь":
            $ rush_1 = True
    
    if not rush_1:
        menu:
            fox "*Судя по рунам, дверь ведёт в местное хранилище артефактов.*"
            "Аккуратно войти":
                fox "*Прямо за дверью была ловушка, хорошо, что я знаю хитрости древних. В конце коридора дверь, мне явно туда.*"
                fox "*О! Ещё одна ловушка. И ещё.*"
                fox "*Наконец добрался до двери.*"
                default explorationDone = False
                default storageExit = False
                while not storageExit:
                    menu:
                        fox "*За ней небольшая комната. Большая часть постаментов пустые, но есть один светящийся и несколько потухших.*"
                        "Осмотреть светящийся постамент":
                            fox "*Сначала я нашёл свитки, а теперь ещё и очки магического зрения. Я сказочно богат!*"
                            menu:
                                "Забрать себе поскорее!!!":
                                    $ storageExit = True
                                    $ findGlasses = True
                        "Осмотреть другие постаменты" if not explorationDone:
                            $ explorationDone = True
                            fox "*Как жаль, тут только разбитые и поломанные артефакты. За них и медяка не выручишь.*"
            "Надо скорее изучить":
                pass
    
    "" "*Тадах*"

    fox "*Вот это был взрыв! Меня спас амулету Айрис. Он весь в трещинах, и выдержит максимум ещё одно использование.*"

    fox "*Айрис... Для шестилетки ты творила чудеса магией, но отправиться к затхлому деду на другой конец континента - было глупостью!*"

    menu:
        fox "*Потолок осыпается, надо уходить!*"
        "Убежать как можно скорее из руин":
            pass
        "Осмотреться, может ещё что-то ценное осталось":
            menu:
                fox "*Больше ничего ценного не могу найти. Потолок уже частично обвалился, надо спешить!*"
                "Убежать":
                    pass
                "Осмотреться. Ну не может быть, чтобы это было всё!":
                    jump badEnding1
    
    fox "*Ну хоть на обратном пути спокойно. Вдалеке вижу башню городской ратуши. Надеюсь, успею до комендантского часа.*"

    return

label ch1ep3:

    fox "*Я в городе. У меня есть ещё около часа, чтобы добраться до дома.*"

    call ch1ep1fz2

    fox "*Возможно, у меня паранойя, но у меня ощущение, что за мной кто-то следит.*"
    if len(visitedPlaces & {"shop1", "shop2"}) > 1:
        fox "*Мало кто знал, чем я на самом деле зарабатываю на хлеб. Но, если они взяли Годрика, это вопрос времени, когда за мной придут.*"
        fox "*Завтра я покину город.*"
    else:
        fox "*Лучше отогнать такие мысли. Не думаю, что я кому-то интересен сейчас.*"
    fox "*М-м-м, запах пряных булочек. Зайду купить их для местной ребятни, они будут в восторге.*"
    fox "Привет, Томас! Мне, как обычно, 12 пряных булочек."
    tom "Балуешь ты оборванцев. О них сегодня спрашивали стражники. Они снова набедокурили."
    fox "Я сам был таким оборванцем, оставшимся на улице без родителей в разгар войны. Сладости дают им почувствовать детство."
    menu:
        tom "И то верно. Может, и себе возьмёшь парочку?"
        "Давай":
            "Да, давай, я их очень давно не ел. Может в этот раз смогу насладиться ими."
        "Не стоит":
            "Не стоит, их вкус напоминают об утрате, которую я хочу забыть."
            tom "Ты об этом раньше не рассказывал."
            fox "И не планирую говорить больше."
    tom "Тогда бывай и приходи ещё!"
    fox "Бывай!"

    fox "*Помню, мы с Айрис часто объедались ими и смотрели на проплывающий в лучах луны Летающий город и мечтали о жизни там.*"
    fox "*И как только я продам свитки, я перееду в Летающий город и заживу свою лучшую жизнь. Айрис от зависти бы лопнула.*"
    "" "*Бдыщ*"
    menu:
        fox "Я с кем-то столкнулся"
        "Смотри куда прёшь!":
            fox "Ты бы смотрел куда прёшь!"
            "???" "А ты бы смотрел, на кого голос повышаешь. Чернь."
        "Эй, ты в порядке?":
            fox "Эй, ты в порядке?"
            "???" "Я-то в порядке, а ты куда спешишь так?"
    
    fox "Ластар. Ты что ли? Поднял бы забрало шлема, а то дальше носа не видишь."
    lastar "За оскорбление гвардейца тебя следовало бы отправить в колодки. Но я скучал по твоей надменной роже."
    fox "Я тоже скучал, друг. Ты когда успел вернуться? Разве военная компания на Фаэдирн уже закончилась?"
    lastar "Нет, не закончилась. У меня особое поручение. Но обсудим не здесь."
    fox "Тогда пошли ко мне. У меня всегда найдётся бутылочка медовухи для старого друга."

    fox "*Что-то не так в этой случайной встрече. Но, может, я себя уже накручиваю?*"
    fox "*Ребятня порадовалась угощению. Ластар не понимает, почему я с ними вожусь. Он уже забыл, как сам был таким же оборванцем.*"

    fox "Так что привело тебя в город?"
    lastar "Это я обязательно расскажу. Но сначала хочу узнать, ты снова был в руинах Инфров?"
    menu:
        "Да, был в руинах.":
            fox "Да, был в руинах."
        "Возможно. Возможно, даже и улов хороший.":
            fox "Возможно. Возможно, даже и улов хороший."
            lastar "Ты весь в каменной пыли. Другие, может, и приняли бы тебя за шахтёра, но я-то знаю, что кирка у тебя для образа висит на поясе."
            fox "Твой зоркий глаз не провести. Да, был в руинах."
    lastar "Знаешь же, что дело небезопасное. Лучше бы в армию пошёл, был бы сейчас как я, гвардейцем."
    fox "Для гвардейца рожей не вышел. А среди магической битвы быть пехотинцем - всё равно, что быть смертником."
    lastar "Опять ты за старое, я должен был стать пехотинцем, но Хавелок приметил меня, и я горд быть его гвардейцем."
    menu:
        "Так почему ты здесь?":
            fox "Так почему ты здесь? Ты говорил про какое-то особое поручение."
        "Меня манят приключения, а не бряцанье оружием.":
            fox "Меня манят приключения, а не бряцанье оружием."
            lastar "Твои приключение рано или поздно закончатся на виселице."
            fox "Если меня поймают. А я не планирую быть пойманным."
            lastar "Ты вообще мало что планируешь."
            fox "Импровизация - мой путь к успеху. Так что за особое поручение?"
    lastar "Хавелок хочет закончить войны."
    menu:
        "Серьёзно?":
            fox "Генерал. Закончить войны. Серьёзно?"
            lastar "Я бы и сам себе не поверил, не знай я генерала. А он поднялся от пехотинца до генерала. Он знает, как тяжело простому народу."
            fox "Когда тебя стал волновать простой народ?"
            lastar "Это сейчас не важно."
        "Все этого хотят.":
            fox "Все этого хотят, но никто не знает, как. Ситуация патовая."
    lastar "У генерала есть план. Он верит, что Инфры владели магией без элизия и что она может изменить всё."
    menu:
        "Ты веришь в эти сказки?":
            fox "Ты веришь в эти сказки?"
        "Может, ты веришь и в подводных чудовищ?":
            fox "Может, ты веришь и в подводных чудовищ?"
    lastar "Я верю в своего генерала."
    lastar "У генерала есть сведенья, что в пещерах, недалеко отсюда, есть руины Инфров."
    fox "*Постой-ка. Так вот почему пещера сегодня не была безлюдной, как я ожидал.*"
    lastar "Это необычные руины. Это затерянная библиотека."
    fox "*Может моя встреча с Ластаром не была случайностью?*"
    menu:
        "Ничего себе.":
            fox "Ничего себе. Не знал, что в здешних окрестностях может быть что-то столь ценное."
            lastar "Я знаю, что ты лукавишь. Если бы в округе ничего ценного не было, ты бы приключений не искал."
        "Я что-то такое слышал.":
            fox "Я что-то такое слышал."
    lastar "Может быть, ты уже побывал там. Годрик говорил, что нашёл для тебя зацепку."
    if "shop1" not in visitedPlaces:
        fox "Ты разговаривал с Годриком? Ты же знаешь его, он малость любит преувеличивать."
    else:
        fox "Я видел, что случилось с его лавкой. Ты к этому имеешь какое-то отношение?"
    lastar "Я с Годриком не разговаривал. Я читал отчёты его ареста и допроса."
    lastar "Мне жаль, что так вышло. Стража перегнула палку. Я об этом доложу генералу, и он накажет причастных."
    fox "Как ты мог? Годрик был и твоим товарищем"
    lastar "Я ничего не мог сделать. Генерал подумал, что, так как я местный, я может узнаю кого-то из списка."
    lastar "Там было и твоё имя, Фокс. Я просто хочу помочь."
    menu:
        "Ну, допустим, я был в библиотеке":
            fox "Ну, допустим, я был в библиотеке."
        "Что ему нужно от меня?":
            fox "Что ему нужно от меня?"
            lastar "Сделать тебя богатым. Если ты сможешь найти руины библиотеки."
            fox "Я бы с радостью помог. Но давай уже завтра. Я устал после долгой дороги."
            menu:
                lastar "Ты уверен, что у тебя сейчас для меня ничего нет?"
                "Уверен. Я бы тебе сказал.":
                    lastar "Я тебе верю. Но не знаю, поверит ли Генерал."
                    jump badEnding2
                "Твоя взяла. Я был в библиотеке Инфров.":
                    fox "Твоя взяла. Я был в библиотеке Инфров."
    lastar "Тогда считай, что ты вытянул счастливый билет. Генерал готов заплатить огромную сумму в золоте за любые уцелевшие свитки."
    fox "Я нашёл их."
    lastar "Это прекрасные новости. Всё было не зря. Я организую завтра встречу с генералом. Приготовься стать богатым."
    fox "*Предчувствие кричит, что я впутываюсь в то, от чего нужно держаться как можно дальше. Но кто не рискует, тот не купается в золоте.*"

    return

label ch1ep1fz2:
    $ wantToExit = False
    $ findExit = False
    $ visitedPlaces = set()
    default currentSituation = ""
    while not wantToExit:
        if "shop1" not in visitedPlaces:
            $ currentSituation = "*Есть время, чтобы сходить к Годрику, узнать, смог бы он найти покупателя на такое или стоит пойти домой.*"
        elif "shop2" not in visitedPlaces:
            $ currentSituation = "*Годрик арестован. Может, Эмиль что-то знает, или стоит пойти домой.*"
        else:
            $ currentSituation = "*Годрик и Эмиль арестованы. Мне нужно срочно домой.*"
        menu:
            fox "[currentSituation]"
            "Сходить к Годрику" if "shop1" not in visitedPlaces:
                $ visitedPlaces.add("shop1")
                fox "*Что здесь произошло? Лавку Годрика невозможно узнать, она разгромлена с особым свирепством.*"
                fox "*Внутри не лучше. Ни одной целой вещи не осталось. Кровавый след ведёт от лавки к двери. Надеюсь, они просто его арестовали.*"
                fox "*Это шаль Годрика. Возьму, чтобы отдать Милдред при встрече. Пусть у неё будет напоминание о нём.*"
                fox "*Нужно скорее уходить отсюда. Не хочу наткнуться на стражу, если они решат вернуться.*"
            "Сходить к Эмилю" if "shop1" in visitedPlaces and "shop2" not in visitedPlaces:
                $ visitedPlaces.add("shop2")
                fox "*Я едва не столкнулся со стражником, выходя из подворотни. Мне повезло, что он смотрел в другую сторону.*"
                fox "*Прямо сейчас лавку Эмиля громит стража. У меня плохое предчувствие. Нужно уходить.*"
                
            "Пойти домой":
                fox "*Хорошо бы поторопиться. С таким уловом не хочется попадаться страже.*"
                $ visitedPlaces.add("center")
                if len(visitedPlaces & {"shop1", "shop2"}) == 2:
                    fox "*Как стража узнала о торговцах? Если бы я не был у Годрика позавчера, это ещё могло бы показаться случайностью.*"
                    menu:
                        fox "*Но два магазина в такой короткий промежуток времени. Их явно кто-то сдал. И если это был кто-то из наших, то я в большой опасности.*"
                        "Поспешить домой!":
                            $ wantToExit = True
                else:
                    menu:
                        fox "*А что, если я сразу же смогу договориться о продаже?*"
                        "Домой!":
                            $ wantToExit = True
                        "Сходить проверить торговца":
                            pass

    return


label badEnding1:
    "" "*Шмяк*"
    fox "*Это конец. Меня придавило камнем, и я не могу двинуться.*"
    "" "Жадность убивает. Конец."
    $ badEndingFlag = True
    return

label badEnding2:
    fox "*Генерал не поверил. Меня арестовали посреди ночи и нашли свитки.*"
    fox "*Знал бы, что всё закончится виселицей, согласился бы на предложение генерала.*"
    "" "Пока Фокс играл в шашки, генерал играл в шахматы. Конец."
    $ badEndingFlag = True
    return